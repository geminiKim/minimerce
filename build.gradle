buildscript {
	ext {
		springBootVersion = '1.5.2.RELEASE'
		gradleDependencyManagementPluginVersion = '1.0.0.RELEASE'
		springIOPlatformVersion = 'Brussels-SR1'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath("io.spring.gradle:dependency-management-plugin:${gradleDependencyManagementPluginVersion}")
	}
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

dependencyManagement {
	imports {
		mavenBom "io.spring.platform:platform-bom:${springIOPlatformVersion}"
	}
}

repositories {
	mavenCentral()
}

dependencies {
	compile('org.springframework.boot:spring-boot-starter-web')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('javax.inject:javax.inject')
	compile('org.hibernate:hibernate-java8')
	compile('com.fasterxml.jackson.datatype:jackson-datatype-jsr310')
	compile('com.querydsl:querydsl-jpa')
	compile('com.querydsl:querydsl-core')
	compile('com.querydsl:querydsl-apt')
	compile('org.apache.commons:commons-lang3')
	compile('com.google.guava:guava')
//	compileOnly('org.projectlombok:lombok')
	runtime('mysql:mysql-connector-java')
	testCompile('org.springframework.boot:spring-boot-starter-test')
}



def queryDslOutput = file("src/main/query-dsl")

sourceSets {
	main {
		java {
			srcDir queryDslOutput
		}
	}
}

task generateQueryDSL(type: JavaCompile, group: 'build') {
	doFirst {
		if (queryDslOutput.exists()) return
		logger.info("Creating `$queryDslOutput` directory")
		if(queryDslOutput.mkdirs()) return
		else throw new InvalidUserDataException("Unable to create `$queryDslOutput` directory")
	}

	source = sourceSets.main.java
	classpath = configurations.compile
	options.compilerArgs = [
			"-proc:only",
			"-processor", "com.querydsl.apt.jpa.JPAAnnotationProcessor"
	]
	destinationDir = queryDslOutput
}
compileJava.dependsOn(generateQueryDSL)

clean {
	delete queryDslOutput
}
